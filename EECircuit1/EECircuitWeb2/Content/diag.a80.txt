; This is a diagnostic for ee8080 CPU
; If all test was passed, all virtual LEDs in port 0FFH are on.
work	equ	8000h
work2	equ	8001h
stack	equ	0f000h
stack2	equ	0e000h

	org	0h
	jmp	start

	org	30h
	mvi	a,60h
	ret

	org	38h
	mvi	a,70h
	ret

	org	40h
start:
	lxi	sp,stack
	mvi b,78h
	mov a,b
	cpi	78h
	jnz	fail
	mvi	a,12h
	cpi	12h
	jnz	fail
	lxi	h,3456h
	mvi	a,34h
	cmp	h
	jnz	fail
	mvi	a,56h
	cmp	l
	jnz	fail
	mvi	a,1
	mvi	c,2
	add	c
	cpi	3
	jnz	fail
	adi	3
	cpi	6
	jnz	fail
	mvi	a,55h
	sta	work
	mvi	a,0aah
	lda	work
	cpi	55h
	jnz	fail
	mvi	a,56h
	lxi	d,0
	lxi	b,work
	stax	b
	mvi	a,0aah
	ldax	b
	cpi	56h
	jnz	fail
	lxi	b,0
	lxi	d,work
	stax	d
	mvi	a,0aah
	ldax	d
	cpi	56h
	jnz	fail

	mvi	a,11h
	sta	work
	mvi	a,22h
	sta	work2
	lhld	work
	mov	a,l
	cpi	11h
	jnz	fail
	mov	a,h
	cpi	22h
	jnz	fail

	lxi	h,4433h
	shld	work
	lda	work
	cpi	33h
	jnz	fail
	lda	work2
	cpi	44h
	jnz	fail

	lxi	d,4321h
	lxi	h,8765h
	xchg
	mov	a,l
	cpi	21h
	jnz	fail
	mov	a,h
	cpi	43h
	jnz	fail
	mov	a,e
	cpi	65h
	jnz	fail
	mov	a,d
	cpi	87h
	jnz	fail

	lxi	b,1122h
	push	b
	lxi	b,0ffffh;
	pop	b
	mov	a,c
	cpi	22h
	jnz	fail
	mov	a,b
	cpi	11h
	jnz	fail

	mvi	a,0ffh
	adi	0ffh
	jnc	fail
	push	psw
	mvi	a,0
	adi	0
	jc	fail
	pop	psw
	jnc	fail

; test pchl
	lxi	h,next
	pchl
	jmp	fail;
next:

; test call
	mvi	a,0cdh
	call	sub
	cpi	0c9h
	jnz	fail

; test all cond
	mvi	a,0
	adi	0
	jnz	fail
	jc	fail
	mvi	a,0
	adi	1
	jz	fail
	mvi	a,0ffh
	adi	1
	jnc	fail
	mvi	a,0feh
	adi	1
	jpo	fail
	mvi	a,0fdh
	adi	1
	jpe	fail
	mvi	a,7fh
	adi	1
	jp	fail
	mvi	a,7eh
	adi	1
	jm	fail

; test c-cond
	mvi	a,0
	adi	0
	cz	sub2
	cpi	2
	jnz	fail
	mvi	a,80h
	adi	80h
	cnz	sub
	cpi	0
	jnz	fail

; test rst
	mvi	a,0
	rst	6
	cpi	60h
	jnz	fail
	rst	7
	cpi	70h
	jnz	fail

; test inr dcr
	mvi	a,0ffh
	mvi	b,01h
	inr	a
	jnz	fail
	dcr	b
	jnz	fail

	mvi	a,7eh
	adi	1
	inr	a
	jc	fail

; test inx dcx
	lxi	b,0ffffh
	inx	b
	mov	a,b
	cmp	c
	jnz	fail
	lxi	h,1
	dcx	h
	mov	a,h
	cmp	l
	jnz	fail

; test XTHL
	lxi	h,1234h
	push	h
	lxi	h,5678h
	xthl
	mov	a,h
	cpi	12h
	jnz	fail
	pop	h
	mov	a,h
	cpi	56h
	jnz	fail

; test SPHL
	lxi	h,stack2
	sphl
	lxi	b,1234h
	push	b
	lxi	h,stack2
	dcx	h
	mov	a,m
	cpi	12h
	jnz	fail





	jmp	success

; test ret
sub:
	mvi	a,0c9h
	ret

; test ret-cond
sub2:
	mvi	a,1
	adi	1
	ret	z
	mvi	a,2
	ret nc
	mvi	a,3
	ret

fail:
	mvi	a,080h
	out	0ffh
	hlt
success:
	mvi	a,0ffh
	out	0ffh
	hlt

	end